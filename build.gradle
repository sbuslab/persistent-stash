plugins {
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'scala'
apply plugin: 'java-library'

group 'com.sbuslab'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/public' }
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'persistent-stash'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'persistent-stash'
                description = 'Persistent stash service'
                url = 'https://github.com/sbuslab/persistent-stash'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'kulikovd'
                        name = 'Dmitry Kulikov'
                        email = 'kulikov.dm@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/sbuslab/persistent-stash'
                    connection = 'scm:https://github.com/sbuslab/persistent-stash.git'
                    developerConnection = 'scm:git@github.com:sbuslab/persistent-stash.git'
                }
            }
        }
    }
    
    if (project.findProperty('publishSonatype')) {
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"

                credentials {
                    username = project.findProperty('nexusUsername') ?: ''
                    password = project.findProperty('nexusPassword') ?: ''
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    api 'com.sbuslab:utils:[1.4.0,1.5.0)'
}
